// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    ext {
        // Sdk and tools
        compileSdkVersion = 29
        minSdkVersion = 21
        targetSdkVersion = 29

        // App dependencies
        constraintLayoutVersion = '2.0.1'
        coroutinesVersion = '1.3.6'
        daggerVersion = '2.28.3-alpha'
        dexterVersion = '6.2.0'
        espressoVersion = '3.3.0'
        glideVersion = '4.11.0'
        gradleVersion = '4.0.1'
        hiltLifecycleViewModelVersion = '1.0.0-alpha02'
        junitVersion = '4.13'
        kotlinVersion = '1.4.0'
        ktxVersion = '1.3.1'
        lifecycleVersion = '2.2.0'
        loggingInterceptorVersion = '4.0.1'
        materialVersion = '1.2.0'
        moshiVersion = '1.10.0'
        navigationVersion = '2.3.0'
        preferenceVersion = '1.1.1'
        recyclerViewVersion = '1.1.0'
        retrofitVersion = '2.8.1'
        roomVersion = '2.2.5'
        signaturePadVersion = '1.2.1'
        supportLibraryVersion = '1.2.0'
        timberVersion = '4.7.1'
    }

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$gradleVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$navigationVersion"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$daggerVersion"
    }
}


plugins {
    id 'com.diffplug.spotless' version '5.3.0'
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects { project ->

    project.tasks.withType(JavaCompile) {
        dependsOn(installGitHooks)
    }

}

spotless {
    kotlin {
        target '**/*.kt'
        ktlint().userData(['disabled_rules': 'no-wildcard-imports', 'max_line_length': 'off'])
    }
}

////////////////////////////////////////////////////////////////////////
//
//  Google Java Format pre-commit hook installation
//

tasks.register('installGitHooks', Copy) {
    from(file('config/hooks/pre-commit-stub')) {
        rename 'pre-commit-stub', 'pre-commit'
    }
    into file('.git/hooks')
    fileMode 0777
}
